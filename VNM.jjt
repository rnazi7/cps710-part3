options {
  IGNORE_CASE=false;
  MULTI=true;	// This will generate one AST class for each non-suppressed non-terminal
  JJTREE_OUTPUT_DIRECTORY="AST";  // This will put all your AST classes in the AST directory
  VISITOR=true;	// This won't be used until the next assignment, but will be needed to make your assignment compile properly
}

PARSER_BEGIN(VNM)

public class VNM {
}

PARSER_END(VNM)

TOKEN_MGR_DECLS : 
{

}

SKIP : {
  " "     
  |   "\t"    
  |   "\n"    
  |   "\r" 
}

TOKEN : 
{
  < LESS: "<">
| < LE:     "<="  >
| < GREAT:  ">"   >
| < GE:     ">="  >
| < EQ:     "=="  >
| < NEQ:    "!="  >
| < IN:     "=in" >
| < NOTIN:  "!in" >
| < PLUS:	  "+"   >
| < MINUS:	"-"   >
| < TIMES:	"*"   >
| < DIV:	  "/"   >
| < AND:	  "&"   >
| < VBAR:	  "|"   >
| < NOT:	  "!"   >
| < ASSGN:	":="   >
| < LRND:	  "("   >
| < LSQU:	  "["   >
| < RRND:	  ")"   >
| < RSQU:	  "]"   >
| < COMMA:	","   >
| < SCOL:	  ";"   >
| < TRUE:	  "#1"  >
| < FALSE:	"#0"  >
| < RANGE:	".."  >
}

TOKEN [IGNORE_CASE]:
{
  < DO:"DO">
| < WHILE:"WHILE">
| < FOR:"FOR">
| < IF:"IF">
| < THEN:"THEN">
| < ELIF:"ELIF">
| < ELSE:"ELSE">
| < FI:"FI">
| < FUNCTION:"FUNCTION">
| < RETURN:"RETURN">
| < END:"END">
| < PRINT:"PRINT">
| < PRINTLN:"PRINTLN">
| < VAR:"VAR">
}

TOKEN : 
{
  < #DIGIT:	["0"-"9"]>
|	< #LOWER:	["a"-"z"]>
|	< #UPPER:	["A"-"Z"]>
|	< #LETTER: ["a"-"z","A"-"Z"]>

// Passes strings1 and strings2.
| < STRING: "\"" (~["\""])* "\"" > 
|	< NUMBER:	(<DIGIT>)+ >
|	< IDNUM:  "#" <LETTER>(<LETTER>|<DIGIT>)* >  
|	< IDBOOL:	"?" <LETTER>(<LETTER>|<DIGIT>)* >
|	< IDVEC:  "v_" (<LETTER>|<DIGIT>)+ >  

}

// Special tokens are saved, but not sent to the parser
SPECIAL_TOKEN : 
{
  <COMMENT_SINGLE: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
}



// ==================================================
//                  PARSER BEGINS
// ==================================================

SimpleNode start	() #void :
{}
{  S()  { return (SimpleNode) (jjtree.popNode()); }
| < EOF > {throw new ParseException("End of File.");}
}

// entry point to parser. Called by TestVNM.java
void S() throws ParseException		 :
{}
{	statement_LL1() ";"
| LOOKAHEAD(identifier() ":=") assign_stat() ";" 
| expression() ";" | boolean_call() ";"
}

//------------------------    STATEMENTS, BODIES AND CLAUSES -----------------------------------

// These are all the statements which can be differentiated from each other 
// with a single lookahead
void statement_LL1()		 :
{}
{	var_decl()
| fn_decl()
| return_stat()
| print_stat()
| println_stat()
| if_stat()
| for_stat()
| while_stat()
}

// These are the statements not at the top level, i.e. inside bodies or clauses
// Maintain structural integrity of assignments and function calls by not factoring them
// and using a lookahead to distinguish between them

void statement()	 :
{}
{	statement_LL1()
| LOOKAHEAD(2) fn_call()
| assign_stat()
}

void body()		 :
{}
{	(statement() ";")*
}

void clause()		 :
{}
{	(statement() ";")+
}

//---------------------------   DECLARATIONS ------------------------------------------------

void var_decl()	 :
{}
{	<VAR> ident_list()
}

void fn_decl()		 :
{}
{	<FUNCTION> identifier() "(" (ident_list())? ")" body() <END>
}

void ident_list()	 :
{}
{	identifier() ("," identifier())*
}


//---------------------------   FUNCTION CALLS AND RETURNS ----------------------------------

// Separate out function calls that can be used in expressions (numbers and vectors),
//     from those used in conditions (boolean).
// While this simplifies some things, it adds ugliness elsewhere, e.g. boolean_call() now must be
//    explicitly looked for in start()... tradeoffs... 
void fn_call()		 :
{}
{	<IDNUM> "(" (exp_list())? ")"
|	<IDVEC> "(" (exp_list())? ")"
}

void boolean_call()		 :
{}
{	<IDBOOL> "(" (exp_list())? ")"
}

void exp_list()	 :
{}
{	expression() ("," expression())*
}

// Since we split function calls by boolean and numerical,
//    we need an explicit condition list
void condition_list() :
{}
{ condition() ("," condition())*
}

void return_stat()	 :
{}
{	<RETURN> returnval()
}

// syntactic lookahead to distinguish conditions and expressions.
//    this is needed since a condition() can begin with an expression()
void returnval()	 :
{}
{	LOOKAHEAD(condition()) condition()
| expression() 
}

void print_stat() :
{}
{	<PRINT> print_list()
}

void println_stat() :
{}
{ <PRINTLN> (print_list())?
}

void print_list()	 :
{}
{	
  (expression() | <STRING> | <IDBOOL>) ("," (expression() | <STRING> | <IDBOOL>))*
}

void assign_stat() :
{}
{ <IDNUM> ":=" expression()
| <IDBOOL> ":=" condition()
| <IDVEC> ":=" vec_const()
}

void if_stat()		 :
{}
{	<IF> condition() <THEN> clause() elifs() (<ELSE> clause())? <FI>
}

void elifs()		 :
{}
{	(<ELIF> condition() <THEN> clause())*
}

void for_stat()	 :
{}
{	<FOR> <IDNUM> <IN> exp_list() <DO> body() <END>
}

void while_stat()	 :
{}
{	<WHILE> condition() <DO> body() <END>
}


//---------------------------   CONDITIONS ---------------------------------------------------

void condition()	 :
{}
{	and_clause() ("|" and_clause())*
}

void and_clause()	 :
{}
{	not_clause() ("&" not_clause())*
}

// syntactic lookahead to distinguish comparisons and conditions.
// use boolean_call() instead of fn_call
void not_clause()	 :
{}
{	"!" not_clause()
| LOOKAHEAD(expression() comparator()) comparison() | "(" condition() ")"
| LOOKAHEAD(2) boolean_call()
| bool_simple()
}

void comparison()	 :
{}
{	expression() comparator() expression()
}

void comparator()	 :
{}
{	"<"
| "<="
| ">"
| ">="
|"=="
| "!="
| "=in"
| "!in"
}


//---------------------------   EXPRESSIONS ------------------------------------------------

void expression()	 :
{}
{	("+" | "-")? product() (("+" | "-") product())*
}

void product()		 :
{}
{	term() (("*" | "/") term())*
}

// use a lookahead to distinguish fn_call from simple_term.
//    simple_term and fn_calls are quite distinct so factoring would be confusing
void term()		 :
{}
{	"(" expression() ")"
| LOOKAHEAD(2) fn_call()
| simple_term()
}

void simple_term()	 :
{}
{	<IDNUM>
| <IDVEC>
| <NUMBER>
| vec_const() 
}

// Again, with splitting out expression and condition productions,
//    in order to have vectors of booleans, we need explicitly add them
//    and we need the syntactic lookahead to distinguish between them
//    (recall that conditions can begin with an expression)
void vec_const()	 :
{}
{	
  LOOKAHEAD("[" expression()) "[" (exp_list())? "]"
| "[" (condition_list())? "]"
}

void identifier()	 :
{}
{	<IDNUM>
| <IDBOOL>
| <IDVEC> 
}

void bool_simple()		 :
{}
{	<IDBOOL>
| <TRUE>
| <FALSE>
}   
   
   